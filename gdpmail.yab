#!/boot/home/config/bin/yab

sub encDec$(s$)
	
	local aux$,i
	
	aux$=""
	for i=1 to len(s$)
		aux$ = aux$ + chr$(eor(asc(mid$(s$,i,1)), 255))
	next i
	
	return(s$)
	
end sub


sub loadOption()
	
	inifile=open(mainAppDir$ + "/gdpmail.ini", "r")
	if not(inifile) then
		CHECK_INTERVAL=300
		user$=""
		pass$=""	
		browser$=""
	else
		line input #inifile l$
		CHECK_INTERVAL=val(l$)
		line input #inifile user$
		line input #inifile pass$
		pass$ = encDec$(pass$)
		line input #inifile browser$
		close inifile
	endif
	
end sub

sub saveOption()
	
	inifile=open(mainAppDir$ + "/gdpmail.ini", "w")
	print #inifile str$(CHECK_INTERVAL)
	print #inifile user$
	print #inifile encDec$(pass$)
	print #inifile browser$
	close inifile	
	
end sub

sub init()
	
	mainAppDir$=peek$("directory")
	CHECK_INTERVAL=300	
	loadOption()
	
	MAX_MESS=200		
	dim id$(MAX_MESS)
	dim title$(MAX_MESS)
	dim from$(MAX_MESS)
	dim summ$(MAX_MESS)
	
	nmess=0	
	
end sub


sub getElem$(txt$,tag$,start)
	
	local tag1$,tag2$,n,m
	
	tag1$="<"+tag$+">"
	tag2$="</"+tag$+">"
	
	n=instr(txt$,tag1$,start)
	if (n>0) then
		m=instr(txt$,tag2$,n+len(tag1$))
		if (m>0) then
			return mid$(txt$,n+len(tag1$),m-n-len(tag1$))
		else
			return ""
		endif
	else
		return ""
	endif
	
end sub

sub getMess()
	
	local cmd$, ret, atom$, b$, lastpos, found
	
	if (user$="" or pass$="") then
		alert translate$("Insert username and password"),"Ok","stop"
		Settings()
		return
	endif
	
	//
	// SAVE GMAIL ATOM INTO /TMP/ATOM
	//
	cmd$ = "wget --no-check-certificate -O /tmp/atom https://" + user$ + ":" + pass$ + "@mail.google.com/mail/feed/atom"
	ret=system(cmd$)
	
	//alert "ret val: " + str$(ret),"ok",""
	
	if (ret=1) then
		alert translate$("Can't download messages ! Is the password correct ?"), translate$("Oh, I'll check it..."),"stop"
		return
	endif
	
	
	//
	// READ THE WHOLE FILE
	//
	open 1,"/tmp/atom"
	atom$=""
	while(!eof(1))
		line input #1 b$
		atom$=atom$+b$
	wend
	close #1
	
	//
	// DO THE PARSING
	// 
	
	nmess=0	
	lastpos=1
	
	repeat
		found=instr(atom$,"<entry>",lastpos)
		if (found>0) then
			
			// OK I'VE FOUND AN ENTRY
			
			lastpos=found+7
			nmess=nmess+1
			
			title$(nmess)=getElem$(atom$,"title",lastpos)
			summ$(nmess)=getElem$(atom$,"summary",lastpos)
			id$(nmess)=getElem$(atom$,"id",lastpos)
			from$(nmess)=getElem$(atom$,"name",lastpos)
			
		endif
	until (found=0)
	
	//	for i=1 to nmess
	//		print "mess " + str$(i)
	//		print "id=" + id$(i)
	//		print "title=" + title$(i)
	//		print "from=" + from$(i)
	//		print "summ=" + summ$(i)	
	//		print "------------------------------------------------"
	//	next i
	
end sub

sub openFile()
	
	aux$  = filepanel "load-file", translate$("Choose Browser"), "/boot/apps/" 
	if (aux$<>"") then
		textcontrol set "browser",aux$
	endif
	
end sub 

sub Settings()
	
	screenWidth = peek("desktopwidth")
	screenHeight = peek("desktopheight")
	
	window open ((screenWidth/2)-170), ((screenHeight/2)-80) to ((screenWidth/2)+170), ((screenHeight/2)+80), "option", translate$("Settings")
	window set "option", "MinimumTo", 340,160
	window set "option", "MaximumTo", 340,160
	
	draw text 20, 40, translate$("Username"), "option"
	textcontrol 120,25 to 240,40, "gmailuser", "", user$, "option"
	draw text 245, 40, "@gmail.com", "option"
	
	draw text 20, 65, translate$("Password"), "option"
	textcontrol 120,50 to 240,65, "gmailpass", "", pass$, "option"
	
	draw text 20, 90, translate$("Check every"), "option"
	textcontrol 120,75 to 240,90, "checkinterval", "", str$(CHECK_INTERVAL), "option"
	draw text 245, 90, translate$("seconds"), "option"
	
	draw text 20, 115, translate$("Browser"), "option"
	textcontrol 120,100 to 240,115, "browser", "", browser$, "option"
	button 245,100 TO 260,115, "btnOpen", "...", "option" 
	
	button 120,135 to 220,130, "saveopt", translate$("Save"), "option" 
	
	dim part$(1)
	
	inloop = true
	while (inloop)
		
		msg$ = message$
		if (split(msg$, part$(), ":|") > 2) then
			PartTwo$ = part$(2)
			PartThree$ = part$(3)
		endif
		
		//		if (msg$ <> "") print msg$
		
		switch msg$
			case "option:_QuitRequested|"
				inloop = false
				break
				
			case "saveopt|"
				CHECK_INTERVAL=val(textcontrol get$ "checkinterval")
				user$=textcontrol get$ "gmailuser"
				pass$=textcontrol get$ "gmailpass"
				browser$=textcontrol get$ "browser"
				saveOption()
				inloop = false				    	    	
				break
				
			case "btnOpen|"
				openFile()
			default
				
				break
				
		end switch
		
	wend
	
	window close "option" 
	
end sub

sub drawFlag()
	
	if (nmess>0) then
		err = draw image 38,28, mainAppDir$ + "/gmailon.jpeg", "winMain"	
	else
		err = draw image 38,28, mainAppDir$ + "/gmailoff.jpeg", "winMain"
	endif
	
end sub

sub showMess(i)
	
	ret=system("InfoPopper --click-command \"" + browser$ + " http://gmail.google.com\" --click-text \"" + translate$("Open Inbox") + "\" --icon " + mainAppDir$ + "/gmailon.jpeg --app \"" + translate$("New mail") + "\" --title \"[" + str$(i) + "/" + str$(nmess) + "] " + translate$("From") + " " + from$(i) + "\" \"" + title$(i) + chr$(10) + summ$(i) + "\"")
	
end sub

sub checkMail()
	
	local lastID$
	
	lastID$=id$(1)
	if (nmess=0) then
		lastID$=""
	endif
	
	getMess()
	
	for i=1 to nmess
		if (id$(i)=lastID$) then
			break
		endif
		showMess(i)
	next i
	
	drawFlag()
	
end sub

sub tellMeAll()
	
	for i=1 to nmess
		showMess(i)
	next i
	
end sub

sub timer()
	
	local h,m,s
	
	h=val(left$(time$,2))
	m=val(mid$(time$,4,2))
	s=val(mid$(time$,7,2))
	
	return h*3600+m*60+s
	
end sub

sub inbox()
	
	local ret	
	ret=system(browser$ + " http://gmail.google.com")
	
end sub

sub about()
	
	alert "GdpMail\n\n(c) Ago 2006 by Perekk perekk@ddmind.com\n" + translate$("English version by Perekk") + "\n\n" + translate$("Written in YAB - Thanks to Team Maui") + " :-)", "Ok","info"	
	
end sub

//
// MAIN LOOP
//


init()

window open 100,100 to 200,160, "winMain", "GgpMail"
window set "winMain", "MinimumTo", 100,60
window set "winMain", "MaximumTo", 100,60

menu translate$("Menu"), translate$("Open Inbox"), "O", "winMain"
menu translate$("Menu"), translate$("Check now"), "C", "winMain"
menu translate$("Menu"), translate$("Tell me again"), "T", "winMain"
menu translate$("Menu"), translate$("Settings..."), "S", "winMain"
menu translate$("Menu"), "--", "", "winMain"
menu translate$("Menu"), translate$("About..."), "", "winMain"

dim part$(1)

lasttimer=-CHECK_INTERVAL

while (not quitting)
	
	msg$ = message$
	if(msg$<>"") print msg$
	//if (split(msg$, part$(), ":|") < 2) dim part$(2)
	
	now=timer()
	if (now<lasttimer) then
		// PASSED MIDNIGHT
		lasttimer=lasttimer-86400
	endif
	
	if (now>lasttimer+CHECK_INTERVAL) then
		lasttimer=now
		checkMail()
	endif
	
	switch msg$
		case "winMain:_QuitRequested|"
			quitting = true
			break
			
		case "winMain:Menu:" + translate$("Open Inbox") + "|"
			inbox()
			break
			
		case "winMain:Menu:" + translate$("Check now") + "|"
			checkMail()
			break
			
		case "winMain:Menu:" + translate$("Tell me again") + "|"
			tellMeAll()
			break
			
		case "winMain:Menu:" + translate$("Settings...") + "|"
			Settings()
			break      
			
		case "winMain:Menu:" + translate$("About...") + "|"
			about()
			break      
			
			
	end switch
	
wend

window close "winMain"
